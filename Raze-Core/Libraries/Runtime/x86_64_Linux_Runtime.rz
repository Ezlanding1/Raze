import String from Strings;
import RazeRuntime from RazeRuntime;

class Runtime extends RazeRuntime
{
	function static int64 Open(string fn)
	{
		return Syscalls.SYS_OPEN(fn, 0, 0u);
	}

	function static Close(uint64 fd)
	{
		Syscalls.SYS_CLOSE(fd as uint);
	}

	function static Sleep(int64 ns)
	{
		int64 seconds = ns / 1000000000;
		int64 nano = ns % 1000000000;
		Syscalls.Timespec ts = new Syscalls.Timespec(seconds, nano);
		Syscalls.SYS_NANOSLEEP(ts, null);
	}

	function static inline uint64 Sbrk(uint64 inc)
	{
		int64 brkLocation = Syscalls.SYS_BRK(0u);

		if (brkLocation < 0)
		{
			return 0u;
		}
		Syscalls.SYS_BRK((brkLocation as uint64) + inc);
		return brkLocation as uint64;
	}

	function unsafe static uint64 Read(uint64 fd, string buffer, uint64 len)
	{
		return Syscalls.SYS_READ(fd as uint, buffer, len) as uint64;
	}


	function static Write(uint64 fd, string str, uint64 len)
	{
	    Syscalls.SYS_WRITE(fd as uint, str, len);
	}

	function static inline uint64 GetStdFd(uint64 code) 
	{
		return code;
	}

	function static uint64 New(uint64 inc)
	{
		return Sbrk(inc);
	}

	function static inline Exit(int errorCode)
	{
		Syscalls.SYS_EXIT(errorCode);
	}
}

class Syscalls
{
	function unsafe static int64 SYS_READ(uint fd, string buf, uint64 count)
	{
		asm {
			alloc RAX;
			alloc EDI;
			alloc RSI;
			alloc RDX;

			MOV RAX, 0;
			MOV EDI, $fd;
			MOV RSI, $buf;
			MOV RDX, $count;
			SYSCALL;

			return RAX;
		}
	}

	function unsafe static int64 SYS_WRITE(uint fd, string buf, uint64 count)
	{
		asm {
			alloc RAX;
			alloc EDI;
			alloc RSI;
			alloc RDX;

			MOV RAX, 1;
			MOV EDI, $fd;
			MOV RSI, $buf;
			MOV RDX, $count;
			SYSCALL;

			return RAX;
		}
	}

	function unsafe static int64 SYS_OPEN(string filename, int flags, uint16 mode)
	{
		asm {
			alloc RAX;
			alloc RDI;
			alloc ESI;
			alloc DX;

			MOV RAX, 2;
			MOV RDI, $filename;
			MOV ESI, $flags;
			MOV DX, $mode;
			SYSCALL;

			return RAX;
		}
	}

	function unsafe static int64 SYS_CLOSE(uint fd)
	{
		asm {
			alloc RAX;
			alloc EDI;

			MOV RAX, 3;
			MOV EDI, $fd;
			SYSCALL;

			return RAX;
		}
	}


	function unsafe inline static int64 SYS_BRK(uint64 brk)
	{
		asm {
			alloc RAX;
			alloc RDI;

			MOV RAX, 12;
			MOV RDI, $brk;
			SYSCALL;

			return RAX;
		}
	}

	function unsafe inline static int64 SYS_EXIT(int error_code)
	{
		asm {
			alloc EDI;

			MOV EDI, $error_code;
			MOV RAX, 60;
			SYSCALL;

			return RAX;
		}
	}


	function unsafe static int64 SYS_ACCESS(string filename, int mode)
	{
		asm {
			alloc RAX;
			alloc RDI;
			alloc ESI;

			MOV RAX, 21;
			MOV RDI, $filename;
			MOV ESI, $mode;
			SYSCALL;

			return RAX;
		}
	}

	class Timespec
	{
		int64 tv_sec;
		int64 tv_usec;

		function Timespec(int64 tv_sec, int64 tv_usec)
		{
			this.tv_sec = tv_sec;
			this.tv_usec = tv_usec;
		}
	}

	function unsafe static int64 SYS_NANOSLEEP(Timespec rqtp, Timespec rmtp)
	{
		asm {
			alloc RAX;
			alloc RDI;
			alloc RSI;

			MOV RAX, 35;
			MOV RDI, $rqtp;
			MOV RSI, $rmtp;
			SYSCALL;

			return RAX;
		}
	}
}
